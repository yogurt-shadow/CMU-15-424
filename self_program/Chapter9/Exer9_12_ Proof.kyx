/* Exported from KeYmaera X v4.9.3 */

Theorem "Exer9_12"

Description "Time-triggered control ping-pong ball. Postcondition 0<=x<=10 with control 4<=x<=5".


Definitions
    Real c;
    Real f;
    Real g;
    Real vm;
End.

ProgramVariables
    Real x;
    Real v;
    Real t;
    Real x0;
    Real v0;
End.

Problem
  c >= 0 & c <= 1 & g > 0 & f > 0 & x >= 0 & x <= 10 & v <= 0 
  ->
  [
      {
         t := 0;
         x0 := x;
         v0 := v;
         {{x' = v, v' = -g,t' = 1 & (x >= 0 & x0 >= 5)}
         ++
         {x' = v, v' = -g, t' = 1 & (x >= 0 & x0 < 5 & x0 + v0 * t - 0.5*g*t^2 <= 5)}}
          {?x = 0; v := -c * v; ++ ?!x = 0;}
         {?x >= 4 & x <= 5 & v > 0 & v^2 + 2*g*x > 2*g*10; v := -f * v; ++ ?!(x>= 4 & x <= 5 & v > 0 & v^2 + 2*g*x > 2*g*10);}
      }*   @invariant(x >= 0 & x <= 10 & (x >= 5 -> v < 0 | 2*g*x + v^2 <= 2*g*10))
      ](x >= 0 & x <= 10)
End.

Tactic "Exer9_12: Proof"
auto
End.

End.