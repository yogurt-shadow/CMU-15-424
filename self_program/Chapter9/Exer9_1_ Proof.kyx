/* Exported from KeYmaera X v4.9.3 */

Theorem "Exer9_1"

Description "Time-triggered ping-pong with upper-bound time".

Definitions
    Real g;
    Real f;
    Real c;
End.

ProgramVariables
    Real x;
    Real v;
    Real t;
End.

Problem
  x >= 0 & x <= 5 & g > 0 & c >= 0 & c <= 1 & f >= 0
  ->
  [
      {
          {?x = 0; v := -c * v; ++ ?!x = 0;}
          {?v > 0 & (v >= g & x + v - 0.5 * g > 5 | v >= 0.5 * g & v < g & v ^ 2 + 2 *g * x > 10 *g | v  < 0.5*g & x + 0.5 * v - 0.125 * g > 5); v := -f * v;
              ++
             ?!(v > 0 & (v >= g & x + v - 0.5 * g > 5 | v >= 0.5 * g & v < g & v ^ 2 + 2 *g * x > 10 *g | v  < 0.5*g & x + 0.5 * v - 0.125 * g > 5));
          }
          t := 0;
          {x' = v, v' = -g, t' = 1 & (t >= 0.5 & t <= 1 & x >= 0)}
      }*   @invariant(x >= 0 & x <= 5)
      ](x >= 0 & x <= 5)
End.

Tactic "Exer9_1: Proof"
implyR(1) ; loop("x>=0&x<=5", 1) ; <(
  QE,
  id,
  composeb(1) ; composeb(1.1) ; composeb(1.1.1) ; assignb(1.1.1) ; choiceb(1) ; andR(1) ; <(
    composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; choiceb(1) ; andR(1) ; <(
      composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; allR(1) ; implyR(1) ; ODE(1),
      testb(1) ; implyR(1) ; allR(1) ; implyR(1) ; ODE(1)
      ),
    testb(1) ; implyR(1) ; choiceb(1) ; andR(1) ; <(
      composeb(1) ; testb(1) ; implyR(1) ; assignb(1) ; allR(1) ; implyR(1) ; solve(1) ; QE,
      testb(1) ; implyR(1) ; allR(1) ; implyR(1) ; solve(1) ; QE
      )
    )
  )
End.

End.