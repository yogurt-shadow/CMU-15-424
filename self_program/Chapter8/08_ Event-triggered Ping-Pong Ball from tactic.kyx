/* Exported from KeYmaera X v4.9.3 */

Theorem "08: Event-triggered Ping-Pong Ball"
Description "8.2.7 Proposition 8.1: Event-triggered ping-pong is safe".

Definitions
  Real H;        /* initial height */
  Real g;        /* gravity */
  Real c;        /* damping coefficient */
  Real f;        /* paddle factor */
End.

ProgramVariables
  Real x, v;     /* height, velocity */
End.
                
Problem
  (0<=x&x<=5 & v<=0) &
  (g>0 & 1>=c&c>=0 & f>=0)
  ->
  [
    {
      { {x'=v,v'=-g&x>=0&x<=5}++{x'=v,v'=-g&x>=5} }
      {?x=0; v:=-c*v; ++ ?(4<=x&x<=5&v>=0); v:=-f*v; ++ ?(x!=0&x<4|x>5);}
    }* @invariant(5>=x&x>=0 & (x=5->v<=0))
  ] (0<=x&x<=5)
End.



Illustration "https://lfcps.org/info/ping-pong-ball-early.png".

Tactic "08: Event-triggered Ping-Pong Ball from tactic"
implyR(1) ; loop("5>=x&x>=0&(x=5->v<=0)", 1) ; <(
  QE,
  QE,
  composeb(1) ; choiceb(1.1) ; choiceb(1.1.1) ; composeb(1.1.0) ; composeb(1.1.1.0) ; assignb(1.1.0.1) ; testb(1.1.0) ; assignb(1.1.1.0.1) ; testb(1.1.1.0) ; testb(1.1.1.1) ; choiceb(1) ; andR(1) ; <(
    solve(1) ; QE,
    solve(1) ; QE
    )
  )
End.

End.